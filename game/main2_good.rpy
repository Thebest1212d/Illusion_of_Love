# --- Початок хорошої гілки ---
label good_end:
    "Я подумав.. Яка ж вона все таки неймовірна..."

    show solomia smiled
    with dissolve

    h "Ех, швидше б додому... трохи залипнути в ігри"

    s"От ти знову про своє! Ти ж так себе виснажиш, як так далі підеш"

    h"Хе-хе"

    "Я завжди надавав перевагу віртуальному світу, а не реальному"

    "Реальність занадто сувора"

    "Та й після смерті матері, ігри були моєю єдиною відрадою"

    show solomia shy
    with dissolve

    s"Ну..Ммм"

    "Я подивився на неї з подивом, вона така мила, коли робить таке обличчя"

    s"Ну то що, пішли до класу?"
    h"Звичайно!"
    show solomia blink
    with dissolve
    s"Тоді чекаю тебе біля школи після уроків"
    "Її посмішка збила мене з пантелику"
    "Я намагався відкинути дивні думки"
    "Соломія пішла до класу"
    hide solomia shy
    with dissolve
    "Мабуть, варто поспішити. Не впевнений, що я правильно зробив"
    jump class
    return


    #---------------------------------------------------------------------------------------------------------------
    # --- Сцена в класі ---
label class:
    scene bg class
    with fade
    show solomia blink
    with dissolve
    h "Знову ці нудні уроки..."
    "Пробурчав я"
    s "Та не бурчи! Давай хоч трохи бадьорості!"
    "Я не люблю школу. Це марна трата часу"
    "Соломія починає підходити..."
    play sound krok1
    show solomia blink at zoomin with dissolve # Анімація переміщення та збільшення

    "Вона підійшла надто близько!"
    h "Ти чого?"
    s "Та так...{w}Нічого.{w} Просто хотіла на тебе поближче подивитися"
    "Я розгубився і не знав, що відповісти"
    show solomia blink at normal with dissolve
    s "Слухай, ти зробив домашнє?"
    h "Звичайно ж ні, списати дасиш?"
    s "Ну не знаю, а що ти мені даси натомість?"
    "Я насупився"
    s "Та жартую я, але з тебе морозиво, домовились?"
    "Як ніби, у мене є вибір..."
    h "Гаразд, домовилися"
    play sound zvonok
    s "Ой, урок починається"
    h "Так, скоро вчитель прийде, ходімо до місць"
    "Як я вже й казав, школа нудна"
    "Коли вже закінчаться ці уроки..."
    stop sound fadeout 2.0
    # Затемнення екрана
    scene dark with dissolve

    centered"Історія, математика, фізика. Як же нудно"
    centered"Я хочу просто поспати"
    centered"Гей!"
    centered"Що це за голос?"
    centered"Агов!"
    centered"ПРОКИНЬСЯ НАРЕШТІ!"

    scene bg class
    with fade
    show solomia blink
    with dissolve
    h "Га? Що таке?"
    s "Уроки вже скінчилося, всі вже пішли"
    h "Ой, вибач, я трохи задрімав"
    show solomia shy with dissolve
    s "Ти такий милий, коли спиш"
    h "Ага, ходімо вже"
    s "Злюка!"
    "Лайно, чого вона причепилась"
    hide solomia blink with dissolve
    jump walk
    return

    # --- Прогулянка після школи ---
label walk:
    scene bg out3day with dissolve
    show solomia blink with dissolve
    s "То що, як ти, справляєшся?"
    h "Ти про що?"
    s "Нелегко, мабуть, самому жити"
    h "Та ні, навпаки, навіть легше"
    h "Маю повну свободу дій"
    h "З їжею важкувато, адже готувати я не дуже добре вмію"
    show solomia shy with dissolve
    s "Ну то...Ти не проти, якщо я тобі зготую щось до школи?"
    s "Просто я готую забагато для себе, а з кимось їсти завжди веселіше!"
    "Що...{w}Мені готуватиме така милаха!"
    "Я трохи почервонів від несподіванки"
    h "Ну...Звичайно я буду радий!"
    show solomia smiled with dissolve
    s "Ну от і домовились, ходімо додому!"
    "Я мовчки посміхнувся. Ну хіба це не чудово?"
    "Ось так проводити час та теревенити ні про що"
    h "Ходімо"
    hide solomia smiled with dissolve
    scene bg house2night
    show solomia blink
    s "Ну ось ми і вдома, дякую що провів мене"
    "Мені тепер якось ніяково"
    h "Звичайно, завжди радий!"
    s "Ну, тоді бувай, до завтра"
    "Соломія підійшла та прошептала на вухо"
    play sound krok1
    show solomia blink at zoomin with dissolve # Анімація переміщення та збільшення

    s "Добраніч.{w} Постарайся сьогодні виспатися.{w} Солодких тобі, {b}гарячих{/b} снів"
    play sound heart
    "Серце зараз з грудей вискочить"
    show solomia smiled at normal with dissolve
    s "Ну, бувай"
    "Моє сердечко такого більше не витримає♥"
    h "Бувай. До завтра"
    hide solomia smiled with dissolve
    "Тепер я не зможу заснути через її слова"
    "Цікаво, що я відчуваю до Соломії"
    "Так, вона є близькою подругою, але я ніколи не думав про неї, як про партнерку"
    "А можливо, ми могли б стати...{w}Ближче"
    "Ех, безнадійний я. Час додому"
    play sound door
    scene room_heronight with dissolve
    "Я дуже сьогодні стомився. Хочу відпочивати"
    "Я впав на ліжко"
    scene dark with fade
    centered"Соломія"
    centered"Хто я для неї?"
    centered"Просто друг?"
    centered"Чи дещо...Більше?"
    centered"Цікаво, коли вона миється у ванній..."
    play sound dusch
    s "[heroname]"
    s "Я хочу стати ще ближче"
    s "[heroname]..."
    "Я нахиляюсь ближче"
    play sound budilnik
    centered"Лайно"
    centered"Ще б трішки. Як завжди на найцікавішому"
    centered"Час прокидатись"
    scene room_heroday with dissolve
    "Треба збиратись у школу"
    jump seconddayg


    ############################################
    #              Другий день                 #
    ############################################
label seconddayg:
    "Все зробивши я пішов у школу"
    play sound door
    "Соломія, мабуть, вже теж вийшла з дому"
    scene bg schoolout with dissolve
    show solomia blink with dissolve
    s "Привітик!"
    h "Привіт"
    s "Як спалося сьогодні?"
    "Я трохи почервонів від свого сну"
    h "Чудово. А тобі як?"
    s "Мені теж добре, пішли до класу, скоро урок почнеться"
    hide solomia blink with dissolve
    scene bg class with dissolve
    show solomia shy with dissolve
    s "Ну...Я приготувала обід, як і обіцяла"
    s "Сподіваюсь, тобі сподобається"
    "Ви берете її судочок з їжею"
    h "Хмм, що у нас тут?"
    h "Омлет та каша"
    "А ну, спробуємо. Смакота!"
    h "Дякую, дуже смачно!"
    s "Я рада, я дуже старалась!"
    "Ось би вона завжди мене так підгодовувала"
    "Її їжа дійсно просто чарівна"
    "Омлет такий пухкий та ніжний, як і її губи"
    "Каша, така ж об'ємна, як і..."
    "Кхм, просто їж не думаючи"

    "Урок скоро почнеться"
    "Можливо, мені варто запросити Соломію погуляти по магазинам"
    "Скоро буде її День Народження..."
    h "Соломія, слухай..."
    menu prop:
        "Запросити її?"
        "Наважитися":
            # --- Запрошення Соломії після школи ---
            h "Соломія, ти не хочеш зі мною після школи піти по магазинам?"
            show solomia think with dissolve
            s "Звичайно! Куди підемо?"
            h "Я хотів би зайти по дорозі додому в один магазин"
            s "Добре, я тоді чекаю тебе під школою"
            play sound zvonok
            h "Ой, вже урок почався. До зустрічі"
            hide solomia think with dissolve
            "Уроки. Знову"
            "От цікаво, можна в майбутньому нічого не робити й жити як заманеться?"
            "Щодо майбутнього..."
            "Чи є в ньому місце для Соломії?"
            play sound heart
            "Мені страшно від своїх почуттів"
            "Я закохався по вуха в неї ще давно"
            "І причина на те досить проста — доброта"
            "Вона дуже мені допомогла в минулому, тому я вирішив, що зроблю її щасливою"
            "Адже, вона така добра та чуйна, як тут не закохатися¿"
            "Але...Чи відчуває вона щось до мене?"

            # --- Сцена під школою з Соломією ---
            scene bg schoolout with dissolve
            show solomia blink with dissolve
            s "Ну то що, пішли?"
            "Ви радісно посміхнулися"
            h "Так, звичайно"
            show solomia smiled with dissolve
            s "А ти чого такий радісний?"
            h "А що не можна?{w} В кінці кінців, я ж йду з такою красунею на побачення"
            show solomia shy with dissolve
            s "Ну ти скажеш, ніяке це не побачення!"
            "Вона так почервоніла, милаха"
            play sound baka
            h "Ой та годі тобі, я ж просто дразнюся"
            s "А що якби це..."
            stop sound fadeout 2.0
            s "Дійсно було б побаченням..."
            play sound heart
            "Я відчув, як я ще більше почервонів"
            "Що мені на це відповісти?"
            h "Пішли вже врешті-решт"
            "ВОНА ЦЕ СКАЗАЛА"
            "Я сам тепер червоний, як рак, трясця"
            stop sound fadeout 2.0

            jump dare
            return
        
        # --- Відмова від запрошення, інша гілка ---
        "Краще не варто":     
            s "Щось сталося?"
            h "Не зважай, то пусте"
            s "Слухай, я хочу зайти в магазин після школи, не хочеш зі мною?"
            h "Я...Не знаю"
            "Мені страшнувато від того, що подумають інші, якщо нас побачать разом"
            s "Чого така невпевненість?"
            h "Я не знаю, що якщо нас побачать інші?"
            s "Ну і що тут такого?"
            h "Ну ми як ніби, не знаю, зустрічаємося"
            h "Я не хочу, щоб поповзли якісь плітки"
            show solomia confident with dissolve
            s "Я не думала, що для тебе аж настільки важлива думка оточуючих"
            s "Хоча на мої почуття ти НІКОЛИ не зважаєш"
            s "Роби як знаєш, мені все одно"
            hide solomia confident with dissolve
            play sound zvonok
            "І що я не те сказав"
            "Невже її так засмутили мої слова?"
            "Треба зосередитись на уроках й додому"
            stop sound fadeout 2.0
            u "І відповідає у нас [heroname]..."
            "Лайно"
            u "Виріш, тоді цю задачу, якщо у тебе є час, щоб літати в хмарах"
            h "Так, учителю"
            "Умова задачі: Якщо опівночі йде дощ, чи можна очікувати, що через 72 години буде сонячна погода?"
            menu zadacha:
                "Ваша відповідь?"
                "Так":
                    u "Неправильна відповідь, берись вже за розум"
                    "Як ніби мені не байдуже"
                    u "Урок закінчено, на сьогодні все"
                    "Нарешті"
                    "Але спершу...."

                "Ні":
                    u "Правильно, молодець"
                    $ score += 1
                    "Добре, хоч в цьому розуміюся"
                    if not achievement.has("secret"):  # Перевіряємо, чи досягнення ще не здобуте
                        achieve secret
                        play sound achievement
                    u "Урок закінчено, ви гарно попрацювали, молодці"
                    "Чудово, а тепер..." 
            jump magazone
            return

# --- Вибір магазин ---
label magazone:
    scene bg out3day with dissolve
    menu magazchoose:
        "Чи варто мені зайти в магазин?"
        "Зайти в місцевий магазин":
            # --- Сцена покупки подарунка ---
            "Напевно, варто вибачитись перед Соломією"
            "Завтра день Святого Валентина й заодно її День Народження"
            "Напевно, варто купити якусь прикрасу й шоколад"
            "І все ж таки. Що я до неї відчуваю?"
            "Мені дуже важко розібратися у своїх почуттях"
            "Але все ж, більше так, ніж ні"
            "І чому це так важко!"
            "Зайшовши в магазин, ви побачили милого медведика й шоколад у формі серця"
            "Як оригінально"
            "Купивши це все, ви повернулися додому"
            scene room_heroday with dissolve
            "Ось і вдома"
            "Час щось поїсти"
            "Я довго думав над почуттями до Соломії"
            "Мені необхідно щось зробити, перебороти страх"
            scene room_heronight with dissolve
            "Страх не давав мені можливості виразити свої почуття"
            "Час йти спати"
            "Ви лягаєте в ліжко та відразу засинаєте"

            # --- Сон та зізнання Соломії ---
            scene dark with dissolve
            show solomia2 think with dissolve 
            "Я стояв перед Соломією, її погляд був холодним і далеким"
            h "Соломія, я хотів сказати тобі... Я люблю тебе"
            "Вона мовчала, а потім зробила крок назад, наче слова, які я щойно вимовив, її налякали"
            s "Не кажи цього. Ти не розумієш... Ми не можемо бути разом"
            h "Чому? Що я зробив не так?"
            "Вона уникала мого погляду, її руки стиснуті в кулаки"
            s "Це не про тебе... Це я. Я просто не можу дозволити собі таке. Не зараз, не з тобою"
            "Я хотів її зупинити, але її образ почав розчинятися в темряві"
            "Вона віддалялася, залишаючи мене самотнім у безмежній пустоті"
            hide solomia2 think with dissolve 
            scene room_heroday with dissolve
            "Я прокинувся, задихаючись. Мої руки тремтіли, а серце болісно калатало"
            h "Це був лише сон... Але чому він здається таким реальним?"
            scene bg schoolcorridor with fade
            "Після уроків я вирішую залишитися трохи довше в школі, щоб зібратися з думками. Коридори майже порожні, лише тихі кроки лунають у тиші"
            "Я не очікував, що побачу її. Соломія стояла біля вікна, дивлячись кудись далеко. Її вигляд був задумливим і трохи сумним"
            show solomia think with dissolve
            play sound heart
            "Моє серце стислося. Щось підштовхнуло мене зробити крок вперед"
            h "Соломія?"
            "Вона здригнулася, наче її раптово вирвали з думок, і обернулася до мене"
            show solomia smiled
            s "Ох, це ти... Ти налякав мене"
            "Вона ніяково усміхнулася, але її очі видавали якусь внутрішню боротьбу"
            h "Все гаразд? Ти виглядаєш трохи... стривоженою"
            "Вона знову подивилася у вікно, стискаючи в руках книгу"
            show solomia shy with dissolve
            s "Я довго думала про це, але... я не можу більше тримати це в собі"
            "Її голос здригнувся, і вона зробила крок до мене. Моя впевненість почала зникати, коли я побачив, як її очі блищать від невисловлених слів"
            show solomia shy at zoomin with dissolve
            "Вона підійшла зовсім близько, так близько, що я міг відчути її легкий подих"
            s "Ти... ти маєш знати. Я... я люблю тебе"
            "Її слова розбили тишу, наче грім серед ясного неба. Моє серце завмерло, і я не знав, що відповісти"
            s "Я боялася сказати це, бо думала, що ти можеш не відчути того ж... Але більше я не можу мовчати"
            "Її руки тремтіли, коли вона простягнула їх, торкнувшись моєї"
            h "Соломія, я навіть не знаю, що сказати. Я думав, що це лише я..."
            s "Ні, це не тільки ти. Я чекала, що ти зізнаєшся, але ти мовчав. І тепер я більше не хочу чекати"
            scene dark with dissolve
            "Вона зробила крок уперед і, злегка піднявшись на носочки, торкнулася своїми губами моїх"
            pause(1.0)
            play sound kiss1
            pause(3.0)
            scene bg schoolcorridor with fade
            show solomia blink with dissolve
            "Поцілунок був несподіваним і водночас таким ніжним, що я відчув, як весь світ зупинився в цей момент"
            if not achievement.has("kiss"):  # Перевіряємо, чи досягнення ще не здобуте
                achieve kiss
                play sound achievement
            "Коли вона відсторонилася, її обличчя було червоним, але вона продовжувала дивитися прямо мені в очі"
            s "Дякую, що не смієшся з мене. Це було важко сказати, але я рада, що зробила це"
            h "Соломія... Я обіцяю, що ніколи не підведу тебе. Ти для мене дуже важлива"
            "Ми залишилися стояти в коридорі, тримаючись за руки. Дзвінок на урок змусив нас розійтися, але я знав, що тепер між нами все змінилося"
            hide solomia blink with dissolve
            "Після уроків ми пішли разом до її будинку"
            scene bg house2day with dissolve
            show solomia smiled with dissolve
            s "Ну ось ми й прийшли, дякую, що провів мене"
            h "Ну то..."
            h "Де моя нагорода?"
            "Як же я хочу ще хоч разочок її поцілувати"
            show solomia shy with dissolve
            s "Ну тоді заплющ очі!"
            h "Ну добре, добре"
            hide solomia shy with dissolve
            scene black with dissolve
            play sound krok1
            "Ви чуєте, як Соломія підійшла ближче"
            "Ви чуєте її дихання, серце, відчуваєте її солодкий запах"
            "Спочатку вона цілуває вас легенько, кінчиками губ"
            play sound kiss1
            "Після цього ви завівшись, притискаєте її тіло ближче"
            "Ви продовжуєте цілуватись ще сильніше, ніби намагаючись передати почуття один одному"
            "Щоправда, повітря закінчується..."
            scene bg house2day with dissolve
            show solomia smiled with dissolve
            "Ви відхекуючись відхиляєтесь від Соломії"
            s "Ну то що, така нагорода підійде?"
            h "Еге ж..."
            "Ви просто не в силах стримати посмішку"
            h "Ну, бувай, Соломіє, до завтра!"
            s "Бувай, якщо треба буде ще {b}нагорода{/b}, звертайся!"
            s "Адже, ми тепер зустрічаємося"
            s "І це робити для пар цілком нормально"
            h "Так, досі, якщо чесно не можу в це повірити, що ми тепер пара"
            s "Як і я"
            hide solomia smiled with dissolve
            play sound krok1
            "Це було щось з чимось"
            "Я радий, що вийшло все саме так"
            "Ох, час йти додому, я вже надто стомився сьогодні"
            scene room_heronight with dissolve
            play sound door
            "Ось я і вдома"
            jump relationship
            return

        #homesecondb знаходиться тут, бо я даю вибір magazchoose саме тут, тому цей кусок коду має бути тут

        "Піти додому":
            scene room_heroday with dissolve
            label homesecondb:   
                play sound door
                "Нарешті вдома"
                menu home2:
                    "Чим би зайнятися?"
                    "Пограти в ігри":
                        "Нарешті гра, треба ще викрутити персонажа. Ти мене вже зачекалася"
                        menu games2:
                            "IoL: Fortune's Treasure":
                                jump gacha_options3
                            "Snake":
                                $ quick_menu = True
                                jump play_snake2

                    "Сісти за уроки":
                        "Гаразд, час трохи за розум взятися"
                        "Але так лінь, добре, треба хоч трохи повчитися"
                        jump lessons2
                        #Уроки вікторина2
                        return


    ############################################
    #                  ГАЧА                    #
    ############################################

    
label gacha_options3:
    menu gacha_options_day3:
        "Виконати 1 витяг":
            $ result = pull_gacha()
            play sound pobeda
            if result['rarity'] == "5★":
                $ rarity_color_start = "{color=#FFA500}"  # Оранжевий
                if result['name'] == "Соломія":
                    if not achievement.has("gacha"):  # Перевіряємо, чи досягнення ще не здобуте
                        achieve gacha  # Нагорода за отримання 5★ персонажа Соломії
                        play sound achievement
            elif result['rarity'] == "4★":
                $ rarity_color_start = "{color=#800080}"  # Фіолетовий
            else:
                $ rarity_color_start = "{color=#0000FF}"  # Синій
            $ rarity_color_end = "{/color}"  # Закриття кольору
            "Ви отримали: [rarity_color_start][result['name']] ([result['rarity']])[rarity_color_end]"
            jump gacha_options_day3

        "Виконати 10 витягів":
            python:
                results = [pull_gacha() for _ in range(10)]
                
                results = [r for r in results if r is not None]
                
                results_text = ", ".join([
                    f"{{color=#FFA500}}{r['name']} ({r['rarity']}){{/color}}" if r['rarity'] == "5★" else
                    f"{{color=#800080}}{r['name']} ({r['rarity']}){{/color}}" if r['rarity'] == "4★" else
                    f"{{color=#0000FF}}{r['name']} ({r['rarity']}){{/color}}"
                    for r in results
                ])

            play sound pobeda
            "Результати 10 витягів: [results_text]"
            jump gacha_options_day3
        
        "Піти спати":
            "Ви так і заснули за комп'ютером"
            jump thirddayb
